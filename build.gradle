plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    modImplementation "dev.smootheez:smootheez-config-lib:${project.scl_version}"
}

processResources{
    inputs.property "version", project.version
    filteringCharset "UTF-8"
    filesMatching("fabric.mod.json"){
        expand project.properties
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

repositories {
    maven {
        url localProperties.getProperty('localRepositoryUrl', '')
    }
}

def targetJavaVersion = project.java_version
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= (10 as String) || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion as Integer)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = '7lCr5INB'
    versionNumber = project.mod_version
    versionName = "${project.mod_name} ${project.mod_version}"
    versionType = 'release'
    uploadFile = remapJar
    gameVersions = ["1.20.4"]
    loaders = ['fabric']
    dependencies {
        required.project 'fabric-api'
        required.project 'smootheez-config-lib'
    }
}

curseforge {
    apiKey = "${System.getenv("CURSEFORGE_TOKEN")}"
    project {
        id = '1020221'
        releaseType = 'release'
        addGameVersion project.minecraft_version
        addGameVersion 'Fabric'
        addGameVersion 'Java 17'

        relations {
            requiredDependency 'fabric-api'
            requiredDependency 'smootheez-config-lib'
        }

        mainArtifact(remapJar) {
            displayName = "${project.mod_name} ${project.mod_version}"
        }
    }
}

tasks.register('publishMod') {
    dependsOn 'modrinth', 'curseforge'
    group = 'publishing'
}